//////////////////////////////////
// Packer Credentials
//////////////////////////////////

resource "local_sensitive_file" "PKRVARS" {
  filename = "./../generated.auto.pkrvars.hcl"
  content  = <<-HEREDOC
  # Generated by Terraform
  azure_credentials = {
    client_id       = "${azuread_application.MAIN.application_id}"
    client_secret   = "${azuread_service_principal_password.MAIN.value}"
    subscription_id = "${data.azurerm_subscription.CURRENT.subscription_id}"
    tenant_id       = "${data.azurerm_subscription.CURRENT.tenant_id}"
  }

  img_resource_group_name = "${azurerm_resource_group.IMAGE.name}"
  tmp_resource_group_name = "${azurerm_resource_group.BUILD.name}"

  shared_image_gallery = {
    subscription_id     = "${data.azurerm_subscription.CURRENT.subscription_id}"
    resource_group_name = "${azurerm_resource_group.GALLERY.name}"
    gallery_name        = "${azurerm_shared_image_gallery.MAIN.name}"
  }
  HEREDOC
}

output "credentials" {
  sensitive = true
  value     = {
    client_id       = azuread_application.MAIN.application_id
    client_secret   = azuread_service_principal_password.MAIN.value
    subscription_id = data.azurerm_subscription.CURRENT.subscription_id
    tenant_id       = data.azurerm_subscription.CURRENT.tenant_id
  }
}

output "resource_groups" {
  value     = {
    shared_image_gallery_resource_group_name = azurerm_resource_group.GALLERY.name
    image_resource_group_name                = azurerm_resource_group.IMAGE.name
    build_resource_group_name                = azurerm_resource_group.BUILD.name
  }
}

output "shared_image_gallery" {
  value = {
    subscription_id     = data.azurerm_subscription.CURRENT.subscription_id
    resource_group_name = azurerm_resource_group.GALLERY.name
    gallery_name        = azurerm_shared_image_gallery.MAIN.name
  }
}